plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'com.vomiter.langmerge'
}

def devRuntimeProject(String path) {
    evaluationDependsOn(path)
    dependencies {
        runtimeOnly files(
                project(path)
                        .tasks.named("devJar")
                        .flatMap { it.archiveFile }
        )
        compileOnly project(path)
    }
    tasks.matching { it.name in ["runClient", "runServer", "runGameTestServer"] }.configureEach {
        dependsOn("${path}:devJar")
    }
}


langMerge {
    modId = mod_id
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

//println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
}

apply from: 'gradle/runs.gradle'
apply from: 'gradle/publishing-local.gradle'

def mixinConfigName = "${mod_id}.mixins.json"
def generatedResourcesDir = layout.buildDirectory.dir("generated/resources").get().asFile

tasks.register("generateMixinConfig", Copy) {
    from("src/main/resources")
    include("mixins.template.json")
    into(generatedResourcesDir)

    rename { " ${mixinConfigName} ".trim() } // 避免不小心空白
}
sourceSets {
    main {
        resources {
            srcDir(generatedResourcesDir)
        }
    }
}
tasks.named("compileJava").configure {
    dependsOn("generateMixinConfig")
}


repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven { url "https://maven.blamejared.com" }
}
apply from: 'gradle/optional-dep.gradle'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")

    annotationProcessor "io.github.llamalad7:mixinextras-common:0.4.1"
    compileOnly       "io.github.llamalad7:mixinextras-common:0.4.1"
    implementation    "io.github.llamalad7:mixinextras-forge:0.4.1"
    jarJar            "io.github.llamalad7:mixinextras-forge:[0.4.1,)"

    implementation fg.deobf("curse.maven:terrafirmacraft-302973:6835820")
    runtimeOnly fg.deobf("curse.maven:patchouli-306770:4636277")
    implementation fg.deobf("curse.maven:farmers-delight-398521:6597298")
    implementation fg.deobf("curse.maven:survivors-abilities-1354890:7281675")

    implementation fg.deobf("curse.maven:beneath-1113980:7400831")
    implementation fg.deobf("curse.maven:my-nethers-delight-1003673:6529919")
}
devRuntimeProject(":survivorsdelight")

def modProps = [
        minecraft_version      : minecraft_version,
        minecraft_version_range: minecraft_version_range,
        forge_version          : forge_version,
        forge_version_range    : forge_version_range,
        loader_version_range   : loader_version_range,
        mod_id                 : mod_id,
        mod_name               : mod_name,
        mod_license            : mod_license,
        mod_version            : mod_version,
        mod_authors            : mod_authors,
        mod_description        : mod_description,
]

tasks.named("processResources", ProcessResources).configure {
    dependsOn("generateMixinConfig")
}
tasks.named('processResources', ProcessResources).configure {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.properties(modProps)

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand modProps + [project: project]
    }

    exclude("mixins.template.json")

}
apply from: 'gradle/mixin.gradle'

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'    : modProps.mod_id,
                'Implementation-Title'   : project.name,
                'Implementation-Version' : project.jar.archiveVersion,
                'Implementation-Vendor'  : modProps.mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'           : "${modProps.mod_id}.mixins.json"
        ])
    }
    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

jarJar.enable()
reobf {
    create("jarJar")
}
tasks.named('jarJar').configure { finalizedBy 'reobfJarJar' }


